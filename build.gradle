plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
  	id "com.matthewprenger.cursegradle" version "1.4.0"
  	id "com.github.breadmoirai.github-release" version "2.2.12"
	id 'com.novoda.build-properties' version '0.4.1'
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or([
                "CURSE_API_KEY"		: "N/A",
                "GITHUB_API_TOKEN"  : "N/A"
        ])
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group

repositories {    
	maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven'
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://dl.bintray.com/ladysnake/libs'
    }
}

dependencies {
	// MC / Fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//CCA - Base
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_components}"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_components}"
	//CCA - Entity
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cardinal_components}"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${cardinal_components}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = "sources"
}

jar {
    from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		// mavenLocal()
	}
}

curseforge {
    if (project.hasProperty('curse_forge_api_key'))
		apiKey = buildProperties.secrets['CURSE_API_KEY'].string

    project {
        id = project.curse_id
        changelog = "Fix: double hand swing when feeding ghost on servers."
        releaseType = "release"

    	addGameVersion project.minecraft_version
    	addGameVersion project.loader
		addGameVersion "Java 8"

		afterEvaluate {
			mainArtifact(remapJar)
			uploadTask.dependsOn(remapJar)
		}
		
        relations {
			requiredDependency 'fabric-api'
			embeddedLibrary 'cardinal-components'
		}
    }

    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

githubRelease {
	owner = "Unrelentless"
	repo = project.repoName
	token = buildProperties.secrets['GITHUB_API_TOKEN'].string
	tagName = "r${project.mod_version}"
	releaseName = "Release ${project.mod_version}"
	targetCommitish = "master"
	body = "Fix: double hand swing when feeding ghost on servers."

	releaseAssets.from("${project.buildDir}/libs")
}